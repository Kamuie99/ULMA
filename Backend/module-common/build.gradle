import org.testcontainers.containers.MySQLContainer
import org.gradle.api.services.BuildServiceParameters
import org.gradle.api.services.BuildService

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.testcontainers:testcontainers:1.19.8'
        classpath 'org.testcontainers:mysql:1.19.8'
        classpath 'mysql:mysql-connector-java:8.0.33'
    }
}

plugins {
    id 'nu.studer.jooq' version '9.0'
    id "org.sonarqube" version "5.0.0.4638"
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-jooq'

    implementation 'org.jooq:jooq:3.19.11'
    implementation 'org.jooq:jooq-codegen:3.19.11'
    jooqGenerator 'org.jooq:jooq-meta:3.19.11'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'mysql:mysql-connector-java:8.0.33'
    runtimeOnly 'com.mysql:mysql-connector-j'
    jooqGenerator 'com.mysql:mysql-connector-j'
    runtimeOnly 'com.h2database:h2'

    implementation 'org.testcontainers:testcontainers'
    implementation 'org.testcontainers:mysql'

    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
}

Provider<MySqlService> dbContainerProvider = project.getGradle()
        .getSharedServices()
        .registerIfAbsent("mysql", MySqlService.class, {})

jooq {
    def activeProfile = System.getProperty('spring.profiles.active', 'prod')

    configurations {
        main {
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                if (activeProfile.contains('test')) {
                    def dbContainer = dbContainerProvider.get().getContainer()
                    jdbc {
                        driver = 'com.mysql.cj.jdbc.Driver'
                        url = dbContainer.jdbcUrl
                        user = dbContainer.username
                        password = dbContainer.password
                    }
                } else {
                    jdbc {
                        driver = 'com.mysql.cj.jdbc.Driver'
                        url = 'jdbc:mysql://j11e204.p.ssafy.io:3306/ulma'
                        user = 'userm'
                        password = 'userm1234'
                    }
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.mysql.MySQLDatabase'
                        inputSchema = 'ulma'
                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'com.ssafy11.ulma.generated'
                        directory = 'build/generated-src/jooq/main'
                    }
                }
            }
        }
    }
}


test {
    useJUnitPlatform()
    systemProperty 'org.testcontainers', 'DEBUG'

    usesService dbContainerProvider
    doFirst {
        def dbContainer = dbContainerProvider.get().container
        systemProperty('spring.datasource.url', dbContainer.jdbcUrl)
        systemProperty('spring.datasource.username', dbContainer.username)
        systemProperty('spring.datasource.password', dbContainer.password)
    }
}

bootJar.enabled = false
jar.enabled = true

sonar {
    properties {
        property "sonar.projectKey", "s11-fintech-finance-sub1_S11P21E204_bcdede1e-ca0a-452b-9519-bb727bd39561"
        property "sonar.projectName", "S11P21E204"
    }
}

abstract class MySqlService implements BuildService<BuildServiceParameters.None>, AutoCloseable {

    private final MySQLContainer container;

    MySqlService() {
        container = new MySQLContainer("mysql:8.0.33")
                .withDatabaseName("ulma")
                .withUsername("root")
                .withPassword("1234");
        container.start();

        // SQL 파일을 컨테이너로 복사
        String schemaFilePath = "C:\\Users\\SSAFY\\workspace\\S11P21E204\\Backend\\module-common\\src\\main\\resources\\schema.sql"
        print(schemaFilePath)
        File schemaFile = new File(schemaFilePath)

        if (schemaFile.exists()) {
            // 컨테이너 내부의 경로로 파일 복사
            container.copyFileToContainer(
                    org.testcontainers.utility.MountableFile.forHostPath(schemaFilePath),
                    "/schema.sql"
            );
            // 복사된 schema.sql 파일을 MySQL에 적용
            container.execInContainer("mysql", "-u", "root", "-p1234", "ulma", "-e", "source /schema.sql");
        }
    }

    @Override
    void close() {
        container.stop();
    }

    MySQLContainer getContainer() {
        return container;
    }
}